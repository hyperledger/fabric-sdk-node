/**
 * Copyright 2018 IBM All Rights Reserved.
 *
 * SPDX-License-Identifier: Apache-2.0
 */

plugins {
    id 'com.github.johnrengelman.shadow' version '2.0.4'
    id 'java-library'
    id 'jacoco'
}

dependencies {
    implementation 'org.hyperledger.fabric-chaincode-java:fabric-chaincode-shim:1.3.0-SNAPSHOT'
    implementation 'org.json:json:20180813'
    testImplementation 'junit:junit:4.12'
    testImplementation 'org.mockito:mockito-core:2.+'
}

repositories {
    jcenter()
    maven {
        url "https://nexus.hyperledger.org/content/repositories/snapshots/"
    }
}

shadowJar {
    baseName = 'chaincode'
    version = null
    classifier = null
    manifest {
        attributes 'Main-Class': 'org.example.Start'
    }
}

jacocoTestCoverageVerification {
    afterEvaluate {
        classDirectories = files(classDirectories.files.collect {
            fileTree(dir: it, exclude:  [
                    'org/example/Start.*'
            ])
        })
    }
    violationRules {
        rule {
            limit {
                minimum = 1.0
            }
        }
    }
}

check.dependsOn jacocoTestReport, jacocoTestCoverageVerification
